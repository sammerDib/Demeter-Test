<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="C13366MppcNarrow" Id="{3e5804d4-6c59-4873-994c-27c7930d1c72}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK C13366MppcNarrow EXTENDS C13336Mppc
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR
	{attribute 'OPC.UA.DA' := '0'}
	_config : C13336MppcConfig;
	
	{attribute 'OPC.UA.DA' := '0'}
	_initDevice : BOOL;
	
	{attribute 'OPC.UA.DA.Access' := '0'}
    InputMppcNarrowStateSignal_1	AT %I* : BOOL;
	
	{attribute 'OPC.UA.DA.Access' := '0'}
    InputMppcNarrowStateSignal_2	AT %I* : BOOL;
	
	{attribute 'OPC.UA.DA' := '0'}
	_itemInit : INT;
	{attribute 'OPC.UA.DA' := '0'}
	_pulseGeneratorInit : TON;
	
	{attribute 'OPC.UA.DA' := '0'}
	_coeAccess : CoeAccess;
	{attribute 'OPC.UA.DA' := '0'}
	_initConfigSerialCom : BOOL;
	{attribute 'OPC.UA.DA' := '0'}
	_serialCom  : SerialCom(Prefix := Const.START_OF_TEXT, Suffix := Const.CARRIAGE_RETURN);
	{attribute 'OPC.UA.DA' := '0'}
	_refSerialCom : REFERENCE TO SerialCom(Prefix := Const.START_OF_TEXT, Suffix := Const.CARRIAGE_RETURN) := _serialCom;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF NOT _raiseProperties THEN
	GetStateSignals();
END_IF

// Init device
SUPER^.InitDevice();

IF InitConfigSerialCom AND MppcActivate THEN
	THIS^.Status := TExecutionState.Ready;
ELSE
    THIS^.Status := TExecutionState.Error;	
END_IF

IF THIS^.Status = TExecutionState.Ready THEN
	SUPER^(Conf := _config, SerialCom := _serialCom, RefSerialCom := _refSerialCom);
END_IF

IF SUPER^._initDeviceBase THEN
	IF NOT InitConfigSerialCom THEN
		ConfigureSerialCom();
	ELSIF THIS^.Status = TExecutionState.Ready THEN			
		_refSerialCom(TxBuffer:= HardwareGlobalVariables.TxBufferMppcNarrow, 
					  RxBuffer:= HardwareGlobalVariables.RxBufferMppcNarrow);
		
		// Start Mppc to off 
		IF NOT _initDevice THEN
			InitDevice();
		END_IF				
	END_IF
END_IF]]></ST>
    </Implementation>
    <Folder Name="Commands" Id="{7bbafd0d-a4eb-4ae1-b41e-ffc8ab9ffa3d}" />
    <Method Name="ConfigureSerialCom" Id="{cbc32d5d-77ea-46f3-b3b6-de2bf7de12ad}">
      <Declaration><![CDATA[METHOD ConfigureSerialCom
VAR_INPUT
END_VAR

VAR
	Status: INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _coeAccess.CoeOperation.Ready THEN
	_logger.Information(_message.Reset().Append('Start configuration com serial').ToString());
END_IF;	

_coeAccess.CoeOperation.Start();
_coeAccess(SerialCommunication := _config.SerialCommunication);
	
IF _coeAccess.CoeOperation.Done THEN
	_coeAccess.SetDone();
	
	_Logger.Information(_message.Reset().
							Append('{').Append(TO_STRING(THIS^.Family)).Append('} : ').
							Append(' - Status : ').Append(TO_STRING(_coeAccess.Status)).ToString());
	InitConfigSerialCom := TRUE;
END_IF
 
IF _coeAccess.CoeOperation.Error THEN	
	_coeAccess.SetError(0);
	
	_Logger.Error(_message.Reset().
							Append('{').Append(TO_STRING(THIS^.Family)).Append('} : ').
							Append(' - Status : ').Append(TO_STRING(_coeAccess.Status)).ToString());
	InitConfigSerialCom := TRUE;
END_IF

THIS^.Status := _coeAccess.Status;
State := _coeAccess.Status;]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{4ab6b2ef-e9ac-484d-913e-229678df6ae0}">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
	Config : C13336MppcConfig;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF Config.IsEnabled THEN
	_config := Config;
	
	SUPER^._mppcConfig.Name := Config.Name;
	SUPER^._mppcConfig.DeviceID := Config.DeviceID;
	SUPER^._mppcConfig.IsEnabled := Config.IsEnabled;
	SUPER^._mppcConfig.LogLevel := Config.LogLevel;
	
	SUPER^.Name := Config.Name;
	SUPER^.DeviceID := Config.DeviceID;	
	THIS^.Family := DeviceFamily.MPPC;
		
	_Logger.Information(_message.Reset().Append('Init ').
							Append('{').Append(TO_STRING(THIS^.Family)).Append('} - ').
							Append('{').Append(TO_STRING(Config.Name)).Append('}').ToString());																													
END_IF	]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetStateSignals" Id="{79811574-efb4-4842-9eac-7e4db7c40849}" FolderPath="Commands\">
      <Declaration><![CDATA[METHOD GetStateSignals
VAR_INPUT
END_VAR
VAR
	{attribute 'OPC.UA.DA' := '0'}
	fibersCollector : STRING;
	{attribute 'OPC.UA.DA' := '0'}
	delimeter : STRING := ';';
	{attribute 'OPC.UA.DA' := '0'}
	stateSignal : STRING;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[
fibersCollector := CONCAT(TO_STRING(EMppcCollector.NARROW), delimeter);

IF NOT InputMppcNarrowStateSignal_1 AND NOT InputMppcNarrowStateSignal_2 THEN
	stateSignal := CONCAT(fibersCollector, TO_STRING(EMppcStateModule.UNRECOVERABLE_ERROR));
ELSIF NOT InputMppcNarrowStateSignal_1 AND InputMppcNarrowStateSignal_2 THEN
	stateSignal := CONCAT(fibersCollector, TO_STRING(EMppcStateModule.AWAITING_COOLING_TEMP_STABLE));
ELSIF InputMppcNarrowStateSignal_1 AND NOT InputMppcNarrowStateSignal_2 THEN
	stateSignal := CONCAT(fibersCollector, TO_STRING(EMppcStateModule.AWAITING_HIGH_VOLTAGE_POWER_SUPPLY_STABLE));
ELSE
	stateSignal := CONCAT(fibersCollector, TO_STRING(EMppcStateModule.MODULE_OPERATING_NORMAL));
END_IF

IF stateSignal <> SUPER^.StateSignalsMsg THEN	
	SUPER^.StateSignalsMsg := stateSignal;
	SUPER^.GetStateSignals();
END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="InitConfigSerialCom" Id="{45d9abd6-4388-48d1-86ae-1cb5bcbf28fb}">
      <Declaration><![CDATA[PROPERTY InitConfigSerialCom : BOOL]]></Declaration>
      <Get Name="Get" Id="{6fc88b2c-ba23-45b7-b5b0-d24ac000cdeb}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[InitConfigSerialCom := _initConfigSerialCom;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{16c9303c-66cd-4265-a6d3-f7f43043e666}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_initConfigSerialCom := InitConfigSerialCom;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="InitDevice" Id="{ece1c118-32b5-4788-9869-84d2f06fddb3}" FolderPath="Commands\">
      <Declaration><![CDATA[METHOD InitDevice
VAR_INPUT
END_VAR
VAR	
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT _initDevice THEN		
	IF _pulseGeneratorInit.Q THEN
		_pulseGeneratorInit(IN := FALSE, PT := T#10MS);
			
		CASE _itemInit OF
			0:  _logger.Information(_message.Reset().
								Append(_config.Name).
								Append(' - Start to on').ToString());		
				_itemInit := 1;
				
			1:  _outputVoltageOn := TRUE;			
				_initDevice := TRUE;
				_itemInit := 0;
		END_CASE
	ELSE
		_pulseGeneratorInit(IN := TRUE, PT := T#500MS);	
	END_IF
END_IF
]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="C13366MppcNarrow">
      <LineId Id="285" Count="1" />
      <LineId Id="251" Count="1" />
      <LineId Id="209" Count="0" />
      <LineId Id="207" Count="0" />
      <LineId Id="211" Count="4" />
      <LineId Id="210" Count="0" />
      <LineId Id="216" Count="0" />
      <LineId Id="204" Count="1" />
      <LineId Id="157" Count="0" />
      <LineId Id="203" Count="0" />
      <LineId Id="145" Count="0" />
      <LineId Id="22" Count="1" />
      <LineId Id="59" Count="0" />
      <LineId Id="136" Count="0" />
      <LineId Id="206" Count="0" />
      <LineId Id="162" Count="0" />
      <LineId Id="164" Count="2" />
      <LineId Id="163" Count="0" />
      <LineId Id="31" Count="0" />
      <LineId Id="150" Count="0" />
    </LineIds>
    <LineIds Name="C13366MppcNarrow.ConfigureSerialCom">
      <LineId Id="6" Count="10" />
      <LineId Id="34" Count="0" />
      <LineId Id="19" Count="7" />
      <LineId Id="36" Count="0" />
      <LineId Id="29" Count="1" />
      <LineId Id="5" Count="0" />
      <LineId Id="49" Count="0" />
      <LineId Id="41" Count="0" />
      <LineId Id="56" Count="0" />
    </LineIds>
    <LineIds Name="C13366MppcNarrow.FB_init">
      <LineId Id="9" Count="6" />
      <LineId Id="17" Count="7" />
      <LineId Id="7" Count="0" />
    </LineIds>
    <LineIds Name="C13366MppcNarrow.GetStateSignals">
      <LineId Id="11" Count="10" />
      <LineId Id="5" Count="0" />
      <LineId Id="31" Count="1" />
      <LineId Id="34" Count="0" />
      <LineId Id="37" Count="0" />
      <LineId Id="30" Count="0" />
    </LineIds>
    <LineIds Name="C13366MppcNarrow.InitConfigSerialCom.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="C13366MppcNarrow.InitConfigSerialCom.Set">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="C13366MppcNarrow.InitDevice">
      <LineId Id="27" Count="0" />
      <LineId Id="29" Count="3" />
      <LineId Id="48" Count="1" />
      <LineId Id="37" Count="0" />
      <LineId Id="39" Count="0" />
      <LineId Id="74" Count="0" />
      <LineId Id="66" Count="1" />
      <LineId Id="43" Count="4" />
      <LineId Id="5" Count="0" />
      <LineId Id="59" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>